import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'be.jlr-home.gradle:plantumlPlugin:0.1.+'
    }
}
//plugins { id "me.champeau.gradle.jmh" version "0.3.0" }
plugins { id "com.bmuschko.nexus"     version "2.3.1" }

apply plugin: 'java'
apply plugin: 'plantuml'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'findbugs'
apply plugin: 'com.bmuschko.nexus'

sourceCompatibility = theCompatibility
targetCompatibility = theCompatibility

group               = theGroup
version             = theVersion
description         = theDescription


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repository.jboss.org/nexus/content/groups/public" }
    maven { url "https://gsphere.genoscope.cns.fr/nexus/content/groups/public" }
}

dependencies {
    testCompile group: 'junit'           ,  name: 'junit'                       ,   version: junitVersion
    compile     group: 'ch.qos.logback'  ,  name: 'logback-classic'             ,   version: logbackVersion
    compile     group: theGroup          ,  name: 'grools-checker'              ,   version: checkerVersion
    compile     group: theGroup          ,  name: 'grools-drools-checker'       ,   version: checkerVersion
    compile     group: theGroup          ,  name: 'genome-properties-parser'    ,   version: gpropparserVersion
}

jar {
    manifest {
        attributes( "Implementation-Title":     theName,
                "Implementation-Version":       theVersion,
                "Implementation-Vendor":        "Genoscope, LABGeM",
                "Product-Name":                 theName,
                "provider":                     "Genoscope",
                "Created-By":                   "LABGeM",
                "relevant":                     "GROOLS Application.",
                "Build-Date":                   new SimpleDateFormat("yyyy-MM-dd").format(new Date()),
                "Build-JDK":                    System.getProperty("java.version"),
                "Build-Gradle":                 project.gradle.gradleVersion
        )
    }
    baseName = theName
}
tasks.withType(JavaForkOptions) {
    jvmArgs  "-Dcom.sun.management.jmxremote.port=19988", "-Dcom.sun.management.jmxremote.ssl=false", "-Dcom.sun.management.jmxremote.authenticate=false", "-Ddrools.mbeans=enabled"
}


plantuml{
    fileFormat = 'SVG'
}
findbugs {
     effort = "max"
 }


modifyPom {
    project {
        name          theName
        description   theDescription
        url           urlBase+urlCore
        inceptionYear '2015'
        scm {
            url                   urlBase+urlCore
            connection            urlBase+urlCore+'.git'
            developerConnection   gitBase+urlCore+'.git'
        }
        licenses {
            license {
                name          'CeCILL-C'
                url           'http://www.cecill.info/licences/Licence_CeCILL-C_V1-en.txt'
                distribution  'repo'
            }
        }
        developers {
            developer {
                id    'jmercier'
                name  'Jonathan MERCIER'
                email 'jmercier@genoscope.cns.fr'
            }
        }
    }
}
 extraArchive {
     sources  = true
     tests    = true
     javadoc  = true
 }
nexus {
    sign                  = false
    repositoryUrl         = 'https://gsphere.genoscope.cns.fr/nexus/content/repositories/releases/'
    snapshotRepositoryUrl = 'https://gsphere.genoscope.cns.fr/nexus/content/repositories/snapshots/'
}
