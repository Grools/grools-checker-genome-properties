/*
* Copyright LABGeM (2014)
*
* author: Jonathan MERCIER
*
* Tis software is a computer program whose purpose is to annotate a complete genome.
*
* Tis software is governed by the CeCILL license under French law and
* abiding by the rules of distribution of free software. You can use,
* modify and/ or redistribute the software under the terms of the CeCILL
* license as circulated by CEA, CNRS and INRIA at the following URL
* "http://www.cecill.info".
*
* As a counterpart to the access to the source code and rights to copy,
* modify and redistribute granted by the license, users are provided only
* with a limited warranty and the software's author, the holder of the
* economic rights, and the successive licensors have only limited
* liability.
*
* In tis respect, the user's attention is drawn to the risks associated
* with loading, using, modifying and/or developing or reproducing the
* software by the user in light of its specific status of free software,
* that may mean that it is complicated to manipulate, and that also
* therefore means that it is reserved for developers and experienced
* professionals having in-depth computer Concept. Users are therefore
* encouraged to load and test the software's suitability as regards their
* requirements in conditions enabling the security of their systems and/or
* data to be ensured and, more generally, to use and operate it in the
* same conditions as regards security.
*
* The fact that you are presently reading tis means that you have had
* Concept of the CeCILL license and that you accept its terms.
*/

package fr.cea.ig.grools;

import fr.cea.ig.grools.model.PriorKnowledge;
import fr.cea.ig.grools.model.FourState;
import fr.cea.ig.grools.model.NodeType;
import fr.cea.ig.grools.model.Conclusion;
import fr.cea.ig.grools.model.KnowledgeStatistics;
import fr.cea.ig.grools.Mode;
import java.util.List;

dialect "mvel"



query allSubKnowledge( PriorKnowledge $parent )
    $childs:= List() from collect ( PriorKnowledge( $parent memberOf partOf ) )
end


query allSubKnowledgeAreTrue( PriorKnowledge $parent, List $childs )
    $childs:= List() from collect ( PriorKnowledge( $parent memberOf partOf ))
    forall( PriorKnowledge( presence == FourState.TRUE ) from $childs )
end


query allSubKnowledgeAreNone( PriorKnowledge $parent, List $childs )
    $childs:= List() from collect ( PriorKnowledge( $parent memberOf partOf ))
    forall( PriorKnowledge( presence == FourState.UNKNOWN ) from $childs )
end


query allSubKnowledgeAreFalse( PriorKnowledge $parent, List $childs )
    $childs:= List() from collect ( PriorKnowledge( $parent memberOf partOf ))
    forall( PriorKnowledge( presence == FourState.FALSE ) from $childs )
end


query allSubKnowledgeAreTrueAndFalse( PriorKnowledge $parent, List $childs )
    $childs:= List() from collect ( PriorKnowledge( $parent memberOf partOf ))
    PriorKnowledge( presence == FourState.TRUE  ) from $childs
    PriorKnowledge( presence == FourState.FALSE  ) from $childs
    forall( PriorKnowledge( ( presence ( == FourState.TRUE || == FourState.FALSE) ) ) from $childs )
end

query knowledgeWithOneParent( PriorKnowledge $parent, List $childs )
    $childs:= List( size > 0 ) from collect ( PriorKnowledge( $parent memberOf partOf && partOf.size == 1))
end

query subKnowledgeHaveFalse( List $childs )
    PriorKnowledge( presence == FourState.FALSE ) from $childs
end


query subKnowledgeHaveTrue( List $childs )
    PriorKnowledge(  presence == FourState.TRUE ) from $childs
end


query subKnowledgeHaveBoth( List $childs )
    PriorKnowledge(  presence == FourState.BOTH ) from $childs
end


query subKnowledgeHaveUnknown( List $childs )
    PriorKnowledge( presence == FourState.UNKNOWN ) from $childs
end

query presentSubknowledgeHaveOneParent( List $childs )
    PriorKnowledge( presence == FourState.TRUE && partOf.size == 1 ) from $childs
end

query allKnowledges( List $knowledgeList )
    $knowledgeList := List() from collect ( PriorKnowledge() )
end

query predictionIsTrueAndFalse( String n )
    Prediction( n := priorKnowledgeId, presence == FourState.TRUE )
    Prediction( n := priorKnowledgeId, presence == FourState.FALSE )
    not(Prediction( n := priorKnowledgeId, presence == FourState.UNKNOWN ))
end


query allKnowledgeStatistics( List $knowledgeStatisticsList )
    $knowledgeStatisticsList := List() from collect ( KnowledgeStatistics() )
end


query specificKnowledgeStatistics( String $priorKnowledgeId )
    $k                      : PriorKnowledge( $priorKnowledgeId := id )
    $knowledgeStatistics    : KnowledgeStatistics( $k := priorKnowledge )
end


query hasKnowledgeName( String $name )
    $knowledges: List() from collect ( PriorKnowledge( name == $name ) )
end


query hasKnowledgeId( String $id )
    $knowledge: PriorKnowledge( id == $id )
end


query knowledgePresenceIsUnknown(  )
    $knowledges:= List() from collect ( PriorKnowledge( presence == FourState.UNKNOWN ) )
end

query knowledgeConclusionIsMissing(  )
    $knowledges:= List() from collect ( PriorKnowledge( conclusion == Conclusion.MISSING ) )
end


query currentMode( )
    $mode : Mode()
end
